# biblionumber
copy_field(_id, biblionumber)

# support
marc_map(099t, support)
lookup(support, "fix/lk_support.txt", sep_char:"|", default:ABS)

# collection
marc_map(995h, ccode.$append)
do list(path:ccode, var:c)
    if all_match(c, "^P")
        copy_field(c,ccode_ref)
    end
end

unless exists(ccode_ref)
    copy_field(ccode.0,ccode_ref)
end

copy_field(ccode_ref, coll_lib1)
lookup(coll_lib1, "fix/lk_coll_lib1.txt", sep_char:"|", default:INC)
copy_field(ccode_ref, coll_lib2)
lookup(coll_lib2, "fix/lk_coll_lib2.txt", sep_char:"|", default:INC)
copy_field(ccode_ref, coll_lib3)
lookup(coll_lib3, "fix/lk_coll_lib3.txt", sep_char:"|", default:INC)
copy_field(ccode_ref, coll_lib4)
lookup(coll_lib4, "fix/lk_coll_lib4.txt", sep_char:"|", default:INC)

# Champ 6XX ?
marc_map(600, f6XX.$append)
marc_map(601, f6XX.$append)
marc_map(604, f6XX.$append)
marc_map(605, f6XX.$append)
marc_map(606, f6XX.$append)
marc_map(607, f6XX.$append)
marc_map(608, f6XX.$append)
marc_map(609, f6XX.$append)
marc_map(615, f6XX.$append)
marc_map(676, f6XX.$append)
marc_map(686, f6XX.$append)

if exists (f6XX)
    set_field (f6XX, 'ok')
else
    add_field (f6XX, 'ko')
end

# On cherche l'agence de catalogage
if marc_match(801b,'595126101')
    add_field(agence_cat, 'Roubaix')
elsif marc_match(801b,'FR-751131015')
    add_field(agence_cat, 'BnF')
elsif marc_match(801b,'CDMAIL')
    add_field(agence_cat, 'CDMAIL')
elsif marc_match(801b,'COLACO')
    add_field(agence_cat, 'COLACO')
elsif marc_match(801b,'ADAV')
    add_field(agence_cat, 'ADAV')    
else    
    add_field(agence_cat, 'Autre')
end

marc_map(801c, date_transaction)

retain(biblionumber, support, f6XX, coll_lib1, coll_lib2, coll_lib3, coll_lib4, record, agence_cat, date_cat, date_transaction)
